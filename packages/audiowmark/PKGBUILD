# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Nils Hilbricht <nils@hilbricht.net>

pkgname=audiowmark
pkgver=0.6.1
pkgrel=1
pkgdesc='Audio and video watermarking'
arch=(x86_64 aarch64)
url='https://uplex.de/audiowmark/'
license=(GPL3)
#depends=(fftw libsndfile libgcrypt zita-resampler mpg123 ffmpeg) #explicit from README
depends=(zita-resampler fftw ffmpeg mpg123) #as namcap sees them
groups=(pro-audio)
source=("$url/releases/$pkgname-$pkgver.tar.gz")
sha256sums=('0b22c4364e111f6a70dac0118a1f67319bce25e57c501d5e55918cdb0c2577fb')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-ffmpeg
  make
}

check() {
  #make in build() creates tests/test-common.sh
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir"/usr install
}
